import Array;
import flixel.FlxG;
import funkin.audio.FunkinSound;
import funkin.audio.VoicesGroup;
import funkin.data.song.SongRegistry;
import funkin.graphics.FunkinSprite;
import funkin.modding.PolymodErrorHandler;
import funkin.modding.base.ScriptedFunkinSprite;
import funkin.modding.base.ScriptedMusicBeatSubState;
import funkin.modding.module.Module;
import funkin.modding.module.ModuleHandler;
import funkin.play.GameOverSubState;
import funkin.play.PauseSubState;
import funkin.play.PlayState;
import funkin.play.PlayStatePlaylist;
import funkin.play.character.CharacterDataParser;
import funkin.play.character.CharacterType;
import funkin.ui.AtlasText;
import funkin.ui.MusicBeatSubState;
import funkin.ui.freeplay.FreeplayState;
import funkin.ui.options.OptionsState;
import funkin.ui.options.PreferencesMenu;
import funkin.util.Constants;
import String;

/**
 * This is a Module that's essentially the "core"
 * of Funker Selector.
 *
 * It handles character replacement, the freeplay icon, and preferences.
 * Save data and character data are handled by the `FS_SaveDataHandler` and `FS_CharacterDataHandler` modules respectively.
 *
 * Modules are loaded in the following order:
 * 1. `FS_SaveDataMigrator`
 * 2. `FS_CharacterDataHandler` and `FS_SaveDataHandler`
 * 3. `FS_FreeplayDJReplace` and `FS_ResultsHandler`
 * 4. `FS_CoreModule`
 *
 * The core module is loaded last to ensure that all the other modules can be accessed properly.
 * NOTE: `FS_FreeplayIcon` and `FS_CharacterMenu` are accessed on demand, so they don't need to be loaded in a specific order.
 */
class FS_CoreModule extends Module
{
  /**
   * The current voice list, used for vocal replacement.
   */
  var voiceList:Array<String>;

  /**
   * Some janky shit for passing the BPM through CharacterMenu don't worry about it.
   */
  var freeplayBPM:Float = null;

  /**
   * The current Character Select SubState.
   * This is set to `null` when not in use.
   */
  var charSelect:MusicBeatSubState = null;

  /**
   * An `AtlasText` object, used for the Character Select button.
   */
  var charText:AtlasText;

  /**
   * The Icon as a `FunkinSprite`.
   */
  var charSelectIcon:FunkinSprite;

  /**
   * MODULES
   * These are modules that are called throughout the script.
   */
  /**
   * The `FS_SaveDataHandler` module.
   */
  var FS_SaveDataHandler:Module = ModuleHandler.getModule('FS_SaveDataHandler');

  /**
   * The `FS_CharacterDataHandler` module.
   */
  var FS_CharacterDataHandler:Module = ModuleHandler.getModule("FS_CharacterDataHandler");

  /**
   * Check the game version, initialize save data, and build the JSON character cache.
   */
  function new()
  {
    super('FS_CoreModule', 1000);

    // Initialize the save data.
    FS_SaveDataHandler.scriptCall('initializeSaveData');

    // Initialize the JSON cache.
    FS_CharacterDataHandler.scriptCall('loadJSONDataCache');

    if (isDebugBuild())
    {
      trace("[Funker Selector] Game is a debug build.");
    }
    else
    {
      trace("[Funker Selector] Game is a release build.");
    }
  }

  /**
   * Gets the BPM of the currently selected song in Freeplay.
   * @param freeplay The Freeplay SubState to use as a dynamic object.
   * @return The BPM.
   */
  function getFreeplayCapsuleBPM(freeplay:Dynamic):Float
  {
    var daSongCapsule:SongMenuItem = freeplay.grpCapsules.members[freeplay.curSelected];
    var songBPM:Float = daSongCapsule?.freeplayData?.songStartingBpm;

    if (daSongCapsule.songText.text == 'Random' && songBPM == null)
    {
      var songMusicData:Null<SongMusicData> = SongRegistry.instance.parseMusicData('freeplayRandom');
      songBPM = songMusicData.timeChanges[0].bpm;
    }

    if (songBPM == null) songBPM = 120;

    return songBPM;
  }

  /**
   * The actual logic for character replacement.
   * For Boyfriend and Dad, it checks to see if a vocal
   * file of the current song exists for the specified character,
   * and replaces the original character's vocals with that.
   *
   * @param characterID The character ID to change into.
   * @param characterType The character type we're using.
   *
   */
  function replaceCharacter(characterID:String = 'bf', characterType:CharacterType = null):Void
  {
    var fs_characterData:Dynamic = FS_CharacterDataHandler.scriptCall('getCharacterData', [characterID]);
    var oldChar:BaseCharacter = null;
    var voiceID:String = fs_characterData?.voiceID ?? characterID;
    var variationSuffix:String = PlayState.instance.currentVariation != 'default' ? '-' + PlayState.instance.currentVariation : '';
    var voiceFile:String = Paths.voices(PlayState.instance.currentSong.id, '-' + voiceID + variationSuffix);

    if (characterID == 'default') return;

    trace('[Funker Selector] Replacing type ' + characterType + ' with id ' + characterID);

    switch (characterType)
    {
      case CharacterType.BF:
        oldChar = PlayState.instance.currentStage.getBoyfriend();
        if (Assets.exists(voiceFile))
        {
          voiceList[0] = voiceFile;
        }
      case CharacterType.GF:
        oldChar = PlayState.instance.currentStage.getGirlfriend();
      case CharacterType.DAD:
        oldChar = PlayState.instance.currentStage.getDad();
        if (Assets.exists(voiceFile))
        {
          voiceList[1] = voiceFile;
        }
      default:
        trace('How did you get here?');
        return;
    }

    // Return if the target character does not exist.
    if (oldChar == null) return;

    if (fs_characterData.characterVariations.length > 0)
    {
      for (variation in fs_characterData.characterVariations)
      {
        if (variation.songID == null || variation.characterID == null)
        {
          continue;
        }

        var songMatch:Bool = false;
        var variationMatch:Bool = false;

        if (variation.songID is Array)
        {
          songMatch = (variation.songID.contains(PlayState.instance.currentSong.id));
        }
        else if (variation.songID is String)
        {
          songMatch = (variation.songID == PlayState.instance.currentSong.id);
        }

        if (variation.songVariation != null)
        {
          if (variation.songVariation is Array)
          {
            variationMatch = (variation.songVariation.contains(PlayState.instance.currentVariation));
          }
          else if (variation.songVariation is String)
          {
            variationMatch = (variation.songVariation == PlayState.instance.currentVariation);
          }
        }
        else
        {
          variationMatch = true; // Assume true if `songVariation` does not exist.
        }

        if (songMatch && variationMatch)
        {
          fs_characterData = FS_CharacterDataHandler.scriptCall('getCharacterData', [variation.characterID]) ?? fs_characterData;

          if (FS_CharacterDataHandler.scriptCall('charJSONCheck', [variation.characterID]))
          {
            trace('[Funker Selector] Variation found: ' + variation.characterID);
            characterID = variation.characterID;
          }
          else
          {
            PolymodErrorHandler.showAlert('Funker Selector Error',
              'The character variation "' + variation.characterID + '" does not exist. The base character ID will be used instead.');
          }
          break;
        }
      }
    }

    // Don't bother replacing the character if both character IDs match.
    if (oldChar.characterId != characterID)
    {
      var charZIndex:Int = oldChar.zIndex;
      var character = CharacterDataParser.fetchCharacter(characterID);

      if (character != null)
      {
        oldChar.destroy();
        character.zIndex = charZIndex;
        PlayState.instance.currentStage.addCharacter(character, characterType);

        // fix for pixel pico and nene
        // really dont wanna do this but i have no other choice lol
        // TODO: pr a fix to funkin.assets or something idk
        if (PlayState.instance.currentStageId != 'schoolPico')
        {
          switch (characterID)
          {
            case 'nene-pixel':
              character.x -= 22;
              character.y -= PlayState.instance.currentStageId != 'schoolErect' ? 203 : 153;
              character.originalPosition.set(character.x, character.y);
              if (PlayState.instance.currentStageId == 'schoolErect') character.scriptCall('addSunsetShaders');
            case 'pico-pixel':
              character.x -= -90;
              character.y -= -18;
              character.cameraFocusPoint.x -= 50;
              character.cameraFocusPoint.y += 10;
              character.originalPosition.set(character.x, character.y);
          }
        }
      }
      else
      {
        PolymodErrorHandler.showAlert('Funker Selector Error', 'Something went wrong replacing the ' + characterType + ' character. How the fuck???');
      }
    }
    else
    {
      trace('[Funker Selector] ID ' + characterID + ' and ID ' + oldChar.characterId + ' match. Not replacing...');
    }

    if (characterType == CharacterType.BF)
    {
      PauseSubState.musicSuffix = fs_characterData?.suffixes?.pauseMusic ?? PauseSubState.musicSuffix;
      GameOverSubState.musicSuffix = fs_characterData?.suffixes?.gameOverMusic ?? GameOverSubState.musicSuffix;
      GameOverSubState.blueBallSuffix = fs_characterData?.suffixes?.blueBall ?? GameOverSubState.blueBallSuffix;
    }

    trace('[Funker Selector] Suffixes\n\nPause Music: ' + PauseSubState.musicSuffix + '\nGame Over Music: ' + GameOverSubState.musicSuffix
      + '\nGame Over SFX: ' + GameOverSubState.blueBallSuffix);
  }

  /**
   * Modified version of buildVocals(); to handle vocal replacement.
   *
   * @param voiceList The supplied array.
   */
  function replaceVocals(voiceList:Array<String>):VoicesGroup
  {
    var currentChart = PlayState.instance.currentChart;
    var result:VoicesGroup = new VoicesGroup();
    var songVoices:Array<String> = currentChart.buildVoiceList();

    result.addPlayerVoice(FunkinSound.load(voiceList[0] ?? songVoices[0]));
    result.addOpponentVoice(FunkinSound.load(voiceList[1] ?? songVoices[1]));

    result.playerVoicesOffset = currentChart.offsets.getVocalOffset(currentChart.characters.player);
    result.opponentVoicesOffset = currentChart.offsets.getVocalOffset(currentChart.characters.opponent);

    return result;
  }

  /**
   * SCRIPT EVENTS
   * These are script events that are triggered by the game.
   * Funker Selector uses these for various things.
   */
  override function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);
    // Cache the sprites for JSON characters.
    // This is only done once.
    if (FS_SaveDataHandler.scriptGet('saveData').preferences.preloadSprites
      && !FS_SaveDataHandler.scriptGet('saveData').preferences.potatoMode) FS_CharacterDataHandler.scriptCall('cacheJSONSparrows');
  }

  override function onUpdate(event:UpdateScriptEvent):Void
  {
    super.onUpdate(event);
    if (FlxG.state.subState is FreeplayState)
    {
      // Open the CharacterMenu substate.
      // We need to make a new one everytime since it gets destroyed when it's closed.
      if (FlxG.keys.justPressed.G && charText.visible && !FlxG.state.subState.busy)
      {
        if (charSelect == null)
        {
          freeplayBPM = getFreeplayCapsuleBPM((FlxG.state.subState));
          charSelect = ScriptedMusicBeatSubState.init('FS_CharacterMenu');
          charSelect.camera = FlxG.state.subState.funnyCam;
          FlxG.state.subState.persistentUpdate = false;
          trace("[Funker Selector] Opening the Character Select SubState...");
          FlxG.state.subState.openSubState(charSelect);
        }
        else
        {
          trace("[Funker Selector] The Character Select SubState is already open!");
        }
      }
    }

    // DEBUG FEATURES
    // These are functions that are only available
    // on debug builds.

    if (isDebugBuild())
    {
      // Force reload the JSON cache with F6.
      if (FlxG.keys.justPressed.F6)
      {
        trace("[Funker Selector] Force reloading JSON cache...");
        FS_CharacterDataHandler.scriptCall("loadJSONDataCache");
      }

      // Write the save data to a JSON file.
      if (FlxG.keys.justPressed.F7)
      {
        trace("[Funker Selector] Writing save data to JSON...");
        FS_SaveDataHandler.scriptCall("debug_dumpSave");
      }

      // Load the save data from a JSON file.
      if (FlxG.keys.justPressed.F8)
      {
        trace("[Funker Selector] Loading save data from JSON...");
        FS_SaveDataHandler.scriptCall("debug_loadSaveFromJSON");
      }
    }
  }

  override function onSubStateOpenEnd(event:SubStateScriptEvent):Void
  {
    super.onSubStateOpenEnd(event);
    var state = event.targetState;
    // Set up the Character Select button.
    if (state is FreeplayState)
    {
      if (charSelect != null)
      {
        trace("[Funker Selector] Character SubState still exists! Resetting...");
        charSelect = null;
      }

      charSelectIcon = ScriptedFunkinSprite.init('FS_FreeplayIcon');
      charSelectIcon.camera = state.funnyCam;
      charSelectIcon.visible = false;

      charText = new AtlasText(170, 300, 'G', 'bold');
      charText.visible = false;
      charText.zIndex = 1001;
      charText.camera = state.funnyCam;

      state.add(charSelectIcon);
      state.add(charText);

      state.dj.onIntroDone.add(function() {
        charSelectIcon.visible = true;
        charSelectIcon.animation.play('intro');
        charSelectIcon.animation.finishCallback = function(_) {
          charSelectIcon.animation.play('idle');
          charText.visible = true;
        };
      });
    }
  }

  override function onStateChangeBegin(event:StateChangeScriptEvent):Void
  {
    super.onStateChangeBegin(event);
    if (event.targetState is PlayState)
    {
      // Resetting Game Over and Pause Menu suffixes.
      // We put this in `onStateChangeBegin()` instead of `onStateChangeEnd()`, otherwise,
      // scripted characters will have their suffixes overridden!
      PauseSubState.musicSuffix = '';
      GameOverSubState.musicSuffix = '';
      GameOverSubState.blueBallSuffix = '';
    }
  }

  override function onStateChangeEnd(event:StateChangeScriptEvent):Void
  {
    super.onStateChangeEnd(event);
    // Options Menu stuff
    if (event.targetState is OptionsState)
    {
      var fs_preferencePage:PreferencesMenu = event.targetState.optionsCodex.addPage('funkerselector', new PreferencesMenu());
      fs_preferencePage.onExit.add(function() {
        FlxG.state.optionsCodex.switchPage('options');
      });

      fs_preferencePage.preferenceDesc = [];
      fs_preferencePage.items.clear();
      fs_preferencePage.preferenceItems.clear();

      var menuItem:TextMenuItem = event.targetState.optionsCodex.pages.get('options')
        .createItem("FUNKER SELECTOR", function() FlxG.state.optionsCodex.switchPage('funkerselector'));
      menuItem.y -= 80;

      fs_preferencePage.createPrefItemCheckbox("Simplify UI", "If enabled, simplifies the UI and disables character sprite caching.", (value) -> {
        FS_SaveDataHandler.scriptGet('saveData').preferences.potatoMode = value;
        FS_SaveDataHandler.scriptCall('writeSaveData');
      }, FS_SaveDataHandler.scriptGet('saveData').preferences.potatoMode);

      fs_preferencePage.createPrefItemCheckbox("Preload Sprites",
        "If disabled, character sparrows will not be cached when the game starts. This will cause stuttering when the Character Menu is opened for the first time.",
        (value) -> {
          FS_SaveDataHandler.scriptGet('saveData').preferences.preloadSprites = value;
          FS_SaveDataHandler.scriptCall('writeSaveData');
          if (value)
          {
            FS_CharacterDataHandler.scriptCall("cacheJSONSparrows");
          }
          else
          {
            // Remove the JSON Characters from memory when the user disables the option.
            FS_CharacterDataHandler.scriptCall("purgeJSONSparrowCache");
          }
        }, FS_SaveDataHandler.scriptGet('saveData').preferences.preloadSprites);

      fs_preferencePage.createPrefItemEnum('Menu SFX', 'Change the SFX used for the Character Menu.',
        ["funkin" => "Funkin' Main Menu", "charSelect" => "Funkin' Character Select"], (value) -> {
          FS_SaveDataHandler.scriptGet('saveData').preferences.preferredSFX = value;
          FS_SaveDataHandler.scriptCall('writeSaveData');
          if (value == "charSelect")
          {
            FunkinSound.playOnce(Paths.sound('CS_confirm'));
          }
          else
          {
            FunkinSound.playOnce(Paths.sound('confirmMenu'));
          }
        }, FS_SaveDataHandler.scriptGet('saveData').preferences.preferredSFX);

      fs_preferencePage.createPrefItemCheckbox("DJ Replacement",
        "If disabled, the Freeplay DJ will not be swapped out for the currently selected character's own DJ.", (value) -> {
          FS_SaveDataHandler.scriptGet('saveData').preferences.djSwapping = value;
          FS_SaveDataHandler.scriptCall('writeSaveData');
        }, FS_SaveDataHandler.scriptGet('saveData').preferences.djSwapping);
    }
  }

  override function onScriptEvent(event:ScriptEvent):Void
  {
    super.onScriptEvent(event);
    // Update the Health Icon in the Character Menu button.
    if (event.type == "FS_EXITED_SUBSTATE" && FlxG.state.subState is FreeplayState)
    {
      charSelect = null;

      if (charSelectIcon != null)
      {
        characterData = CharacterDataParser.fetchCharacterData(FS_SaveDataHandler.scriptGet('saveData').characterIDs.bf);
        currentCharacterIcon = charSelectIcon.scriptGet('currentCharacterIcon');

        if (currentCharacterIcon.characterId != characterData?.healthIcon?.id)
        {
          charSelectIcon.scriptCall('spawnIcon');
        }
      }
    }
  }

  override function onCountdownStart(event:CountdownScriptEvent):Void
  {
    super.onCountdownStart(event);
    if (PlayState.instance?.currentStage == null
      || PlayStatePlaylist.isStoryMode
      || PlayState.instance.isMinimalMode
      || PlayState.instance.isChartingMode) return;

    voiceList = [null, null];

    replaceCharacter(FS_SaveDataHandler.scriptGet('saveData').characterIDs.bf, CharacterType.BF);
    replaceCharacter(FS_SaveDataHandler.scriptGet('saveData').characterIDs.gf, CharacterType.GF);
    replaceCharacter(FS_SaveDataHandler.scriptGet('saveData').characterIDs.dad, CharacterType.DAD);
    PlayState.instance.currentStage.refresh();

    trace('[Funker Selector] Voice List:\n\nPlayer: ' + voiceList[0] + '\nOpponent: ' + voiceList[1]);
    PlayState.instance.vocals.stop();
    PlayState.instance.vocals = replaceVocals(voiceList);
  }

  /**
   * HELPER FUNCTIONS
   * These are functions that are simply nice to have. They make the code cleaner.
   */
  /**
   * Whether or not the game is a debug build.
   * @return Bool
   */
  public function isDebugBuild():Bool
  {
    return Constants?.DEBUG_BUILD ?? false;
  }
}
