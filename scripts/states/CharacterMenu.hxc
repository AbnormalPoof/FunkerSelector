import flixel.FlxG;
import flixel.text.FlxText;
import flixel.tweens.FlxEase;
import flixel.tweens.FlxTween;
import flixel.util.FlxTimer;
import funkin.audio.FunkinSound;
import funkin.data.freeplay.player.PlayerRegistry;
import funkin.graphics.FunkinCamera;
import funkin.graphics.FunkinSprite;
import funkin.modding.PolymodErrorHandler;
import funkin.modding.module.ModuleHandler;
import funkin.play.GameOverSubState;
import funkin.play.PauseSubState;
import funkin.play.character.BaseCharacter;
import funkin.play.character.CharacterDataParser;
import funkin.play.components.HealthIcon;
import funkin.save.Save;
import funkin.ui.MusicBeatSubState;
import funkin.ui.charSelect.Lock;
import funkin.ui.freeplay.charselect.PlayableCharacter;
import funkin.util.WindowUtil;
import funkin.util.assets.DataAssets;
import haxe.Json;
import haxe.ds.StringMap;
import Std;

/**
 * This is a substate that acts as a Character Selection screen.
 */
class CharacterSelectSubState extends MusicBeatSubState {
	var charName:FlxText;
	var charDesc:FlxText;
	var selected:FlxText;
	var numberThing:FlxText;

	var leftDifficultyArrow:FunkinSprite;
	var rightDifficultyArrow:FunkinSprite;
	var keyBox:FunkinSprite;
	var newGraphic:FunkinSprite;

	// NOTE: This is either a `FunkinSprite`, or a `Lock`
	// depending on if "Simplify UI" is enabled or not.
	var lockedChill:Null;

	var unlockSound:FunkinSound;

	var hotkeyCam:FunkinCamera;
	var uiCam:FunkinCamera;
	var spriteCam:FunkinCamera;

	/**
	 * A map containing every character.
	 */
	var characterMap:StringMap<String, Array<String, String>> = new StringMap();

	/**
	 * A filtered character array for the current page.
	 * We still use this array since `CharacterMenu` is built around Arrays.
	 */
	var filteredKeys:Array<String> = [];

	// NOTE: For writing, access characterIDs directly. This is only for reading.
	var characterIDsArray:Array<String> = [characterIDs.bf, characterIDs.dad, characterIDs.gf];

	/**
	 * An array of unlock animations we've seen already.
	 * This ensures the unlock animation only plays once per save file.
	 */
	var seenUnlocks:Array<String> = [];

	/**
	 * A map of all the locked characters.
	 */
	var lockedCharMap:StringMap<String> = new StringMap();

	/**
	 * The JSON character cache, which is retrieved from `charHandler`.
	 */
	var jsonCharacterCache:StringMap<String, Dynamic> = ModuleHandler.getModule("CharacterHandler").scriptGet('jsonCharacterCache');

	// For the top text in the menu.
	var topText = ["PLAYABLE CHARACTERS", "OPPONENT CHARACTERS", "GIRLFRIEND CHARACTERS"];
	var topTextColor = [0xFF2DB2D2, 0xFFAF66CE, 0xFFA7004C];

	/**
	 * Pages.
	 * 
	 * This is just an array, their index is used to control a lot of things in the menu.
	 */
	var pages:Array<String> = ['bf', 'dad', 'gf'];

	var charIcon:HealthIcon;
	var iconGrid:Array<HealthIcon> = [];

	var charIndex:Int = 0;
	var pageIndex:Int = 0;

	/**
	 * The current song BPM in Freeplay.
	 */
	var songBPM:Float = ModuleHandler.getModule("CharacterHandler").scriptGet('freeplayBPM');

	/**
	 * The current character ID.
	 */
	var curCharID:String = 'default';

	var characterSprite:BaseCharacter;

	/**
	 * These are retrieved from the save file.
	 */
	var characterIDs:Dynamic = Save.instance.modOptions.get("FunkerSelector");

	var simplifyUI:Bool = Save.instance.modOptions.get("FunkerSelectorSettings").potatoMode;
	var preferredSFX:String = Save.instance.modOptions.get("FunkerSelectorSettings").preferredSFX;

	/**
	 * If enabled, the menu will stop responding to inputs.
	 * This is used for the unlock animation
	 */
	var busy:Bool = false;

	public function new() {
		super();
	}

	public override function create():Void {
		super.create();

		if (Save.instance.modOptions.get("FunkerSelector-SeenChars") == null) {
			Save.instance.modOptions.set("FunkerSelector-SeenChars", seenUnlocks);
			Save.instance.flush();
		} else {
			seenUnlocks = Save.instance.modOptions.get("FunkerSelector-SeenChars");
		}

		trace("SEEN UNLOCK ANIMATIONS: " + seenUnlocks);

		setupUIStuff();
		initChars();
		updateFilteredKeys();
		jumpToCurSelected();
	}

	public override function update(elapsed:Float):Void {
		super.update(elapsed);

		conductorInUse.update();

		numberThing.text = "< " + (charIndex + 1) + " / " + filteredKeys.length + " >";
		numberThing.screenCenter(0x01);

		if (!busy)
			handleKeyShit();
	}

	function beatHit():Void {
		bopDaThing();
		if (characterSprite != null)
			characterSprite.dance();
	}

	/**
	 * Initializing character data for use in the substate itself.
	 * 
	 * Characters use a JSON file and are loaded from `data/funkerSelector/` and
	 * then parsed, they can specify Game Over music, Blue Ball, 
	 * and Pause Menu music suffixes.
	 * If no character type is specified in the JSON, it is pushed
	 * to the Boyfriend character list. 
	 * If no character ID exists in the file (or the character ID is invalid), the character will be skipped. 
	 */
	function initChars():Void {
		var funkerJSONs:Array<String> = DataAssets.listDataFilesInPath('funkerSelector/');
		var data:Dynamic = null;

		for (funker in funkerJSONs) {
			trace('[Funker Selector] Fetching JSON data for ' + funker);

			var data:Dynamic = jsonCharacterCache.get(funker);
			if (data == null)
				continue;

			// If the "characterID" field was not found.
			if (data.characterID == null) {
				PolymodErrorHandler.showAlert('Funker Selector JSON Parsing Error',
					'In "' + Assets.getPath(Paths.json('funkerSelector/' + funker)) +
					'":\n\nThe "characterID" field was not found. This character will be skipped to prevent any issues.');
				continue;
			}

			// If the Character ID is "default"
			if (data.characterID == 'default') {
				PolymodErrorHandler.showAlert('Funker Selector JSON Parsing Error',
					'In "'
					+ Assets.getPath(Paths.json('funkerSelector/' + funker))
					+ '":\n\nThe specified Character ID ('
					+ data.characterID
					+
					') is set to "default"! Please change it, since it will conflict with the "default" character object in the menu.\nThis character will be skipped to prevent any issues.');
				continue;
			}

			// If the specified Character ID does not exist in data/characters/
			if (!charJSONCheck(data.characterID)) {
				PolymodErrorHandler.showAlert('Funker Selector JSON Parsing Error',
					'In "'
					+ Assets.getPath(Paths.json('funkerSelector/' + funker))
					+ '":\n\nThe specified Character ID ('
					+ data.characterID
					+ ') is invalid. This character will be skipped to prevent any issues.');
				continue;
			}

			if (data.mustUnlock && data.unlockMethod != null) {
				if (!isCharacterUnlocked(data.characterID, data.unlockMethod)) {
					trace('[Funker Selector] Character ID ' + data.characterID + ' is locked!');
					lockedCharMap.set(data.characterID, data);
				} else {
					if (seenUnlocks.indexOf(data.characterID) == -1) {
						trace('[Funker Selector] Character ID ' + data.characterID + ' was unlocked!');
						lockedCharMap.set(data.characterID, data);
					}
				}
			}

			if (data.characterType == null)
				data.characterType = 'bf';
			characterMap.set(data.characterID, [funker, data.characterType]);
		}
	}

	/**
	 * Updates the `filteredKeys` array based on the current `pageIndex`.
	 */
	function updateFilteredKeys():Void {
		trace('[Funker Selector] Updating filteredKeys');
		filteredKeys = ['default']; // Ensures 'default' is always the first thing.

		tempArray = [];

		for (key in characterMap.keys()) {
			tempArray.push(key);
		}

		iHateMyLife = tempArray.filter(function(shit) {
			characterType = characterMap.get(shit)[1];
			switch (pageIndex) {
				case 0:
					return characterType == 'bf' || characterType == 'player';
				case 1:
					return characterType == 'dad' || characterType == 'opponent';
				case 2:
					return characterType == 'gf';
			}
		});

		iHateMyLife.sort(function(a:String, b:String):Int {
			nameA = CharacterDataParser.parseCharacterData(a).name.toUpperCase();
			nameB = CharacterDataParser.parseCharacterData(b).name.toUpperCase();

			if (nameA < nameB) {
				return -1;
			} else if (nameA > nameB) {
				return 1;
			} else {
				return 0;
			}
		});

		filteredKeys = filteredKeys.concat(iHateMyLife);
	}

	/**
	 * Make a super awesome cool icon grid!
	 * @param spacing The spacing of the icons, I guess.
	 * 
	 * This is disabled if "Simplify UI" is turned on in the Options Menu.
	 */
	function createIconGrid(spacing:Int, ?alpha:Float = null):Void {
		var charIconData = curCharID != 'default' ? CharacterDataParser.parseCharacterData(curCharID) : null;
		var x = (FlxG.width - Math.ceil(FlxG.width / spacing) * spacing) / 2;
		var y = (FlxG.height - Math.ceil(FlxG.height / spacing) * spacing) / 2;
		for (i in 0...Math.ceil(FlxG.height / spacing)) {
			for (j in 0...Math.ceil(FlxG.width / spacing)) {
				var icon = new HealthIcon('dad', 0);
				icon.x = x + j * spacing;
				icon.y = y + i * spacing;
				icon.alpha = (alpha != null ? alpha : 0.2);

				if (charIconData != null) {
					icon.configure(charIconData.healthIcon);
				} else {
					icon.configure('AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA');
				}

				add(icon);
				iconGrid.push(icon);
				icon.camera = spriteCam;
			}
		}
	}

	/**
	 * Check if the JSON file in `data/characters/` ACTUALLY exists.
	 * 
	 * I wrote this function because OpenFL Assets is case-insensitive
	 * on Windows and macOS. This is for an edge case where the JSON filename
	 * in data/characters/ has different capitalization than the Character ID
	 * specified in the Funker Selector JSON. Because OpenFL is case insensitive, it will
	 * always return true, which confuses Funker Selector!
	 * 
	 * This was reported in issue [#1](https://github.com/AbnormalPoof/FunkerSelector/issues/1)!
	 * 
	 * This should hopefully solve any and all issues like that one.
	 * 
	 * @param string The character ID we're comparing.
	 * @return Returns a true or false if the file exists.
	 */
	function charJSONCheck(string:String):Bool {
		if (Assets.exists(Paths.json('characters/' + string))) {
			var characterFiles = DataAssets.listDataFilesInPath('characters/');
			return characterFiles.indexOf(string) != -1;
		}
		return false;
	}

	/**
	 * Wether or not the character should be shown.
	 * @param characterID The character ID we're using.
	 * @param unlockType The method we're using to determine if this character is unlocked.
	 * @return Bool
	 * 
	 * Unlock Methods:
	 * - playableCharacter: Checks if the associated playable character is unlocked.
	 * - song: Checks if the song has been beaten on the specified difficulty list.
	 * - storyWeek: Checks if the week has been beaten on the specified difficulty list.
	 */
	function isCharacterUnlocked(characterID:String, unlockMethod:Dynamic):Bool {
		switch (unlockMethod.type) {
			case 'playableCharacter':
				if (PlayerRegistry.instance.isCharacterOwned(characterID)) {
					var ownerID:String = PlayerRegistry.instance.getCharacterOwnerId(characterID);
					var playableCharacter:PlayableCharacter = PlayerRegistry.instance.fetchEntry(ownerID);
					return playableCharacter != null && playableCharacter.isUnlocked();
				}
			case "song":
				return Save.instance.hasBeatenSong(unlockMethod.songID, unlockMethod.difficultyList);
			case 'storyWeek':
				return Save.instance.hasBeatenLevel(unlockMethod.levelID, unlockMethod.difficultyList);
			default:
				return false;
		}
	}

	/**
	 * Configuring the JSON character, setting up position, scale, flipping, etc.
	 */
	function configureChar(data:Null):Void {
		var position = data.position != null ? data.position : [0, 0];
		var scale = data.scale != null ? (data.isPixel ? data.scale * 6 : data.scale) : (data.isPixel ? 6.0 : 1.0);
		characterSprite.x += position[0];
		characterSprite.y += position[1];
		characterSprite.flipX = data.flipX != null ? data.flipX : false;
		characterSprite.scale.set(scale, scale);
		characterSprite.antialiasing = data.isPixel != null ? !data.isPixel : true;
	}

	/**
	 * Set up UI stuff like the BG and text.
	 */
	function setupUIStuff():Void {
		spriteCam = new FunkinCamera('SpriteCam');
		FlxG.cameras.add(spriteCam, false);
		spriteCam.bgColor = 0x0;

		uiCam = new FunkinCamera('UIElements');
		FlxG.cameras.add(uiCam, false);
		uiCam.bgColor = 0x0;

		hotkeyCam = new FunkinCamera('HotkeyMenu');
		FlxG.cameras.add(hotkeyCam, false);
		hotkeyCam.bgColor = 0x0;
		hotkeyCam.visible = false;

		bg = new FunkinSprite(0, 0);
		bg.makeSolidColor(FlxG.width, FlxG.height, 0xFF000000);
		bg.alpha = 0;
		bg.camera = spriteCam;

		selectText = configureText(null, null, [170, 10], 50, Paths.font("PhantomMuff.ttf"));
		selectText.screenCenter(0x01);
		selectText.camera = uiCam;

		numberThing = configureText(null, null, [170, 60], 45, Paths.font("PhantomMuff.ttf"));
		numberThing.color = 0xFFE3E1E1;
		numberThing.camera = uiCam;

		selected = configureText(null, '(SELECTED)', [720, 220], 50, Paths.font("PhantomMuff.ttf"));
		selected.color = 0xFF53FF38;
		selected.visible = false;
		selected.camera = uiCam;

		hotkeyText = configureText(null, 'D - Hotkeys', [990, 15], 45, Paths.font("PhantomMuff.ttf"));
		hotkeyText.color = 0xFFE3E1E1;
		hotkeyText.camera = uiCam;

		hotkeys = configureText(null,
			'F - Reset everything to default.\n\nJ - Jump to the currently selected character.\n\nQ - Switch to the previous page.\n\nE - Switch to the next page.\n\nESC - Close this.',
			[350, 80], 40, Paths.font("PhantomMuff.ttf"));
		hotkeys.camera = hotkeyCam;
		hotkeys.wordWrap = true;
		hotkeys.fieldWidth = 600;

		charName = configureText(null, null, [170, 110], 70, Paths.font("PhantomMuff.ttf"));
		charName.camera = uiCam;

		leftDifficultyArrow = FunkinSprite.createSparrow(20, 300, 'storymenu/ui/arrows');
		leftDifficultyArrow.animation.addByPrefix('idle', 'leftIdle0');
		leftDifficultyArrow.animation.addByPrefix('press', 'leftConfirm0');
		leftDifficultyArrow.animation.play('idle');
		leftDifficultyArrow.scale.set(1.5, 1.5);
		leftDifficultyArrow.camera = uiCam;

		rightDifficultyArrow = FunkinSprite.createSparrow(1200, leftDifficultyArrow.y, 'storymenu/ui/arrows');
		rightDifficultyArrow.animation.addByPrefix('idle', 'rightIdle0');
		rightDifficultyArrow.animation.addByPrefix('press', 'rightConfirm0');
		rightDifficultyArrow.animation.play('idle');
		rightDifficultyArrow.scale.set(1.5, 1.5);
		rightDifficultyArrow.camera = uiCam;

		if (!simplifyUI) {
			lockedChill = FunkinSprite.createSparrow(200, 250, 'funkerSelector/locked_character');
			lockedChill.animation.addByPrefix('idle', 'LOCKED MAN0', 24, false);
			lockedChill.animation.addByPrefix('denied', 'cannot select0', 24, false);
			lockedChill.animation.addByPrefix('death', 'locked man explodes0', 24, false);
			lockedChill.animation.play('idle');
			lockedChill.animation.finishCallback = function(_) {
				lockedChill.offset.set();
				lockedChill.animation.play('idle');
			};
			lockedChill.scale.set(1.2, 1.2);
		} else {
			lockedChill = new Lock(300, 270, FlxG.random.int(1, 9));
			lockedChill.onAnimationComplete.add(function(_) {
				lockedChill.playAnimation('idle');
			});
			lockedChill.playAnimation('idle');
			lockedChill.scale.set(2, 2);
		}
		lockedChill.camera = spriteCam;
		lockedChill.visible = false;

		newGraphic = FunkinSprite.createSparrow(840, 250, 'freeplay/freeplayCapsule/new');
		newGraphic.animation.addByPrefix('new', 'NEW notif', 24);
		newGraphic.animation.play('new');
		newGraphic.camera = uiCam;
		newGraphic.scale.set(1.8, 1.8);
		newGraphic.visible = false;

		keyBox = new FunkinSprite(0, 0);
		keyBox.makeSolidColor(600, 600, 0xFF000000);
		keyBox.camera = hotkeyCam;
		keyBox.alpha = 0.8;
		keyBox.screenCenter();

		charIcon = new HealthIcon('dad', 0);
		charIcon.camera = uiCam;

		unlockSound = new FunkinSound();
		unlockSound.loadEmbedded(Paths.sound('funkerSelector/charUnlock'));
		unlockSound.volume = 1;

		add(bg);
		if (!simplifyUI)
			createIconGrid(150, 0.2);
		add(leftDifficultyArrow);
		add(rightDifficultyArrow);
		add(selectText);
		add(numberThing);
		add(selected);
		add(hotkeyText);
		add(charIcon);
		add(charName);
		add(lockedChill);
		add(keyBox);
		add(hotkeys);
		add(newGraphic);

		WindowUtil.setWindowTitle('Friday Night Funkin\' - Funker Selector Menu');

		FlxTween.tween(bg, {alpha: 0.5}, 0.5, {ease: FlxEase.quartOut});

		trace('[Funker Selector] UI has been set up.');

		conductorInUse.forceBPM(songBPM);
	}

	/**
	 * Bopping the icons, because why not?
	 */
	function bopDaThing():Void {
		if (charIcon != null) {
			if (charIcon.width > charIcon.height) {
				charIcon.setGraphicSize(Std.int(charIcon.width + (150 * charIcon.size.x * 0.2)), 0);
			} else {
				charIcon.setGraphicSize(0, Std.int(charIcon.height + (150 * charIcon.size.y * 0.2)));
			}

			charIcon.angle += 1;

			charIcon.updateHitbox();
			charIcon.updatePosition();
		}
	}

	/**
	 * Flash the screen with a specific color.
	 */
	function flashScreen(color:FlxColor, duration:Float, ?alpha:Float = 1) {
		var duration:Float = duration;
		var alpha:Float = alpha;

		var white = new FunkinSprite(0, 0);
		white.makeSolidColor(FlxG.width, FlxG.height, color);
		white.alpha = 0;
		white.camera = uiCam;
		this.add(white);

		FlxTween.tween(white, {alpha: alpha}, duration, {
			ease: FlxEase.quadOut,
			onComplete: function(twn:FlxTween) {
				FlxTween.tween(white, {alpha: alpha}, duration, {
					ease: FlxEase.quadOut,
					onComplete: function(twn:FlxTween) {
						this.remove(white);
					}
				});
			}
		});
	}

	/**
	 * Reponsible for handling inputs in the menu.
	 */
	function handleKeyShit():Void {
		if (FlxG.keys.justPressed.ESCAPE) {
			if (hotkeyCam.visible) {
				hotkeyCam.visible = false;
			} else {
				FunkinSound.playOnce(Paths.sound('cancelMenu'));

				// Scripted characters overwrite the suffixes when
				// they're shown. So we want to manually reset them!
				PauseSubState.musicSuffix = '';
				GameOverSubState.musicSuffix = '';
				GameOverSubState.blueBallSuffix = '';

				WindowUtil.setWindowTitle('Friday Night Funkin\'');
				close();
			}
		}

		if (!hotkeyCam.visible) {
			if (FlxG.keys.justPressed.LEFT || FlxG.keys.justPressed.RIGHT) {
				if (preferredSFX == "charSelect") {
					FunkinSound.playOnce(Paths.sound('CS_select'), 0.2);
				} else {
					FunkinSound.playOnce(Paths.sound('scrollMenu'), 0.4);
				}
				charIndex = (charIndex + (FlxG.keys.justPressed.RIGHT ? 1 : -1) + filteredKeys.length) % filteredKeys.length;
				curCharID = filteredKeys[charIndex];
				updateCharInfo();
			}

			leftDifficultyArrow.animation.play(FlxG.keys.pressed.LEFT ? 'press' : 'idle');
			rightDifficultyArrow.animation.play(FlxG.keys.pressed.RIGHT ? 'press' : 'idle');

			if (FlxG.keys.justPressed.Q || FlxG.keys.justPressed.E) {
				if (preferredSFX == "charSelect") {
					FunkinSound.playOnce(Paths.sound('CS_select'), 0.2);
				} else {
					FunkinSound.playOnce(Paths.sound('scrollMenu'), 0.4);
				}
				pageIndex = (pageIndex + (FlxG.keys.justPressed.E ? 1 : -1) + pages.length) % pages.length;
				updateFilteredKeys();
				charIndex = 0;
				curCharID = filteredKeys[charIndex];
				updateCharInfo();
			}

			if (FlxG.keys.justPressed.J) {
				if (preferredSFX == "charSelect") {
					FunkinSound.playOnce(Paths.sound('CS_select'), 0.2);
				} else {
					FunkinSound.playOnce(Paths.sound('scrollMenu'), 0.4);
				}
				jumpToCurSelected();
			}

			if (FlxG.keys.justPressed.F) {
				var shouldPlaySound:Bool = false;
				if (characterIDs.bf != 'default') {
					characterIDs.bf = 'default';
					shouldPlaySound = true;
				}
				if (characterIDs.dad != 'default') {
					characterIDs.dad = 'default';
					shouldPlaySound = true;
				}
				if (characterIDs.gf != 'default') {
					characterIDs.gf = 'default';
					shouldPlaySound = true;
				}
				if (shouldPlaySound) {
					FunkinSound.playOnce(Paths.sound('ranks/great'), 0.4);
					selected.visible = selectedTextVisibility();
					Save.instance.modOptions.set("FunkerSelector", characterIDs);
					Save.instance.flush();

					// Updating the array with the latest changes
					characterIDsArray = [characterIDs.bf, characterIDs.dad, characterIDs.gf];
				}
			}

			if (FlxG.keys.justPressed.D) {
				hotkeyCam.visible = true;
			}

			if (FlxG.keys.justPressed.ENTER) {
				var shouldConfirm:Bool = false;
				if (curCharID != characterIDsArray[pageIndex]) {
					if (!lockedCharMap.exists(curCharID)) {
						switch (pageIndex) {
							case 0:
								if (characterIDs.bf != curCharID) {
									characterIDs.bf = curCharID;
									shouldConfirm = true;
								}
							case 1:
								if (characterIDs.dad != curCharID) {
									characterIDs.dad = curCharID;
									shouldConfirm = true;
								}
							case 2:
								if (characterIDs.gf != curCharID) {
									characterIDs.gf = curCharID;
									shouldConfirm = true;
								}
						}
					}
				}
				if (shouldConfirm) {
					confirmThing();
					selected.visible = selectedTextVisibility();
					newGraphic.visible = false;
					Save.instance.modOptions.set("FunkerSelector", characterIDs);
					Save.instance.flush();

					// Updating the array with the latest changes
					characterIDsArray = [characterIDs.bf, characterIDs.dad, characterIDs.gf];
				} else {
					if (preferredSFX == "charSelect") {
						FunkinSound.playOnce(Paths.sound('CS_locked'), 0.4);
					} else {
						FunkinSound.playOnce(Paths.sound('cancelMenu'));
					}
					flashScreen(0xFFFF0000, 0.03, 0.1);
					if (lockedChill != null) {
						if (!simplifyUI)
							lockedChill.animation.play('denied', true);
						else
							lockedChill.playAnimation("clicked", true);
					}
				}
			}
		}
	}

	/**
	 * Jumps to the currently selected character.
	 */
	function jumpToCurSelected():Void {
		// In the event that the saved character ID no longer exists, we fallback to default.
		if (!characterMap.exists(characterIDsArray[pageIndex]) && characterIDsArray[pageIndex] != 'default') {
			trace('[Funker Selector] Saved character ID doesn\'t exist! Resetting to default.');
			switch (pageIndex) {
				case 0:
					characterIDs.bf = 'default';
				case 1:
					characterIDs.dad = 'default';
				case 2:
					characterIDs.gf = 'default';
			}
			// Save everything
			Save.instance.modOptions.set("FunkerSelector", characterIDs);
			Save.instance.flush();

			// Updating the array with the latest changes
			characterIDsArray = [characterIDs.bf, characterIDs.dad, characterIDs.gf];
		}
		charIndex = filteredKeys.indexOf(characterIDsArray[pageIndex]);
		curCharID = characterIDsArray[pageIndex];
		updateCharInfo();
	}

	function confirmThing():Void {
		if (preferredSFX == "charSelect") {
			FunkinSound.playOnce(Paths.sound('CS_confirm'), 0.4);
		} else {
			FunkinSound.playOnce(Paths.sound('confirmMenu'));
		}
		if (!simplifyUI) {
			characterData = jsonCharacterCache.get(curCharID);
			animation = characterData?.characterMenu?.selectedAnim != null ? characterData.characterMenu.selectedAnim : 'hey';
			if (characterSprite != null)
				characterSprite.playAnimation(animation, true, true);
		}
		if (charIcon?.hasAnimation('winning')) {
			// Play the winning animation if it's available.
			charIcon?.playAnimation('winning');

			// I wanted this really cool effect where the icons in the icon grid would also
			// play the winning animation!
			// But let's not play that if "Simplify UI" is turned on.
			if (!simplifyUI && iconGrid != null) {
				for (i in 0...iconGrid.length) {
					var icon = iconGrid[i];
					new FlxTimer().start(0.01 * i, function() {
						icon?.playAnimation('winning');
					});
				}
				new FlxTimer().start(0.3, function() {
					for (i in 0...iconGrid.length) {
						var icon = iconGrid[i];
						new FlxTimer().start(0.01 * i, function() {
							icon?.playAnimation('idle');
						});
					}
				});
			}
		}

		flashScreen(0xFFFFFFFF, 0.03, 0.1);
	}

	function selectedTextVisibility():Bool {
		switch (pageIndex) {
			case 0:
				return curCharID == characterIDs.bf;
			case 1:
				return curCharID == characterIDs.dad;
			case 2:
				return curCharID == characterIDs.gf;
			default:
				return false;
		}
	}

	/**
	 * Helper function for configuring an FlxText
	 * @param object The FlxText object
	 * @param text The text itself
	 * @param offsets The offsets as an array
	 * @param size The size of the text
	 * @param font The font
	 * @return A FlxText object
	 */
	function configureText(object:FlxText, ?text:String = "", ?offsets:Array<Int>, ?size:Int = 38, ?font:String = null):FlxText {
		if (text == null)
			text = "";
		if (offsets == null)
			offsets = [0, 0];
		if (size == null)
			size = 38;
		if (object == null)
			object = new FlxText(0, 0, 0, "PLACEHOLDER");

		object.text = text;
		object.x += offsets[0];
		object.y += offsets[1];
		if (font != null)
			object.setFormat(font, size, null, 'center');

		return object;
	}

	/**
	 * Does a really fucking cool unlock animation.
	 */
	function doUnlockAnimation():Void {
		busy = true;
		FlxG.sound.music?.fadeOut();
		if (!simplifyUI) {
			lockedChill.offset.set();
			lockedChill.animation.play('idle', true);
		} else {
			lockedChill.playAnimation('idle', true);
		}
		FlxTween.tween(uiCam, {alpha: 0}, 1, {
			ease: FlxEase.quartOut,
			onComplete: function(twn:FlxTween) {
				unlockSound.play(true);
				new FlxTimer().start(1.45, function() {
					if (!simplifyUI) {
						lockedChill.offset.set(170, 220);
						lockedChill.animation.play('death');
						lockedChill.animation.callback = function(name, frame) {
							if (name == 'death' && frame == 36) {
								flashScreen(0xFFFFFFFF, 0.05, 1);
								new FlxTimer().start(0.025, function() {
									updateCharInfo();
									if (selected.visible)
										selected.visible = false;
									newGraphic.visible = true;
									uiCam.shake(0.01, 0.2);
									spriteCam.shake(0.01, 0.2);
									FlxG.state.subState.funnyCam.shake(0.01, 0.2);
								});
								if (characterSprite != null && !characterSprite.visible) {
									characterSprite.visible = true;
								}
								busy = false;
								uiCam.alpha = 1;
								FlxG.sound.music?.fadeIn();
							}
						}
					} else {
						lockedChill.playAnimation('unlock');
						lockedChill.onAnimationFrame.add(function(name, frame) {
							if (name == 'unlock' && frame == 36) {
								flashScreen(0xFFFFFFFF, 0.05, 1);
								new FlxTimer().start(0.025, function() {
									updateCharInfo();
									if (selected.visible)
										selected.visible = false;
									newGraphic.visible = true;
									lockedChill.playAnimation('idle', true);
									uiCam.shake(0.01, 0.2);
									spriteCam.shake(0.01, 0.2);
									FlxG.state.subState.funnyCam.shake(0.01, 0.2);
								});
								busy = false;
								uiCam.alpha = 1;
								FlxG.sound.music?.fadeIn();
							}
						});
					}
				});
			}
		});
	}

	/**
	 * Update character information.
	 */
	function updateCharInfo():Void {
		if (characterSprite != null)
			remove(characterSprite);

		if (charDesc != null)
			remove(charDesc);

		selectText.text = topText[pageIndex];
		selectText.color = topTextColor[pageIndex];
		selectText.screenCenter(0x01);
		selected.visible = selectedTextVisibility();

		newGraphic.visible = false;

		charDesc = new FlxText(600, 300, 0, "PLACEHOLDER");
		charDesc.wordWrap = true;
		charDesc.fieldWidth = 550;
		charDesc.camera = uiCam;

		if (curCharID != 'default') {
			characterInfo = characterMap.get(curCharID);
			ownerJSONname = characterInfo[0];
			characterData = jsonCharacterCache.get(ownerJSONname);
			charIconData = (curCharID != 'default') ? CharacterDataParser.parseCharacterData(curCharID) : null;
		}

		// If "Simplify UI" is enabled in the Options Menu, the sprites will not load.
		if (!simplifyUI) {
			characterSprite = CharacterDataParser.fetchCharacter(curCharID, true);
			if (characterSprite != null) {
				characterSprite.dance();
				if (characterData.characterMenu != null) {
					configureChar(characterData.characterMenu);
				}
				add(characterSprite);
				characterSprite.camera = spriteCam;
			}
		}

		switch (curCharID) {
			case 'default':
				charIcon.configure('AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA');
				charName.text = "DEFAULT";
				configureText(charDesc, "This is the Default character defined by the song.", [-20, 0], 50, Paths.font("PhantomMuff.ttf"));
			default:
				if (lockedCharMap.exists(curCharID)) {
					charName.text = "LOCKED";

					if (characterSprite != null)
						characterSprite.visible = false;

					var unlockHint = characterData.description != null
						&& characterData.description.unlockCondition != null ? "\n\nUnlock Condition: " + characterData.description.unlockCondition : "";

					configureText(charDesc, "This character is locked! I wonder who it could be..." + unlockHint, null, null, Paths.font("PhantomMuff.ttf"));
				} else {
					charName.text = charIconData?.name;
					charIcon.configure(charIconData?.healthIcon);
					charIcon.size.set(1, 1);
					charIcon.setPosition(550, 160);

					var descText:String = (characterData.description?.text != null) ? characterData.description.text : "No description was specified in the JSON file.";
					var offsets:Array<Int> = [
						characterData.description?.offsets != null ? characterData.description?.offsets[0] : 0,
						characterData.description?.offsets != null ? characterData.description?.offsets[1] : 0
					];

					configureText(charDesc, descText, offsets, characterData.description?.size, Paths.font("PhantomMuff.ttf"));
				}
		}

		// Making it not look so awkward when "Simplify UI" is turned on.
		if (simplifyUI || curCharID == 'default') {
			charIcon?.setPosition(250, 270);
			charIcon?.size.set(2, 2);
		}

		if (!simplifyUI) {
			for (icon in iconGrid) {
				if (curCharID != 'default' && !lockedCharMap.exists(curCharID)) {
					icon?.configure(charIconData?.healthIcon);
				} else {
					icon?.configure('AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA');
				}
			}
		}

		if (lockedCharMap.exists(curCharID)) {
			if (charIcon != null)
				charIcon.visible = false;
			lockedChill.visible = true;
		} else {
			if (charIcon != null)
				charIcon.visible = true;
			lockedChill.visible = false;
		}

		charName.screenCenter(0x01);
		selected.y = charDesc.y - 60;

		add(charDesc);

		// Do the unlock animation if we haven't seen it yet.
		if (lockedCharMap.exists(curCharID) && isCharacterUnlocked(curCharID, characterData.unlockMethod)) {
			if (seenUnlocks.indexOf(curCharID) == -1) {
				lockedCharMap.remove(curCharID);
				seenUnlocks.push(curCharID);
				Save.instance.modOptions.set("FunkerSelector-SeenChars", seenUnlocks);
				Save.instance.flush();
				doUnlockAnimation();
			}
		}

		trace('[Funker Selector] Updated character information.');
	}
}
